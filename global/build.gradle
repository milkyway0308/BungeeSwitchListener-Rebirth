plugins {
    id("java")
    id('org.jetbrains.kotlin.jvm') version "1.4.32"
    id("maven-publish")
    id("com.github.johnrengelman.shadow")
    id("skywolf46.wolfradle")
    id("org.sonarqube")
}


group = "skywolf46"
version = properties["version"] as String

repositories {
    mavenCentral()
}


dependencies {
    bungeeApi()
    wolfyUtil("1.40.1")
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: '1.5.0'
    compileOnly group: 'io.netty', name: 'netty-all', version: '4.1.63.Final'

    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.7.1")
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.7.1")
    testImplementation("org.junit.platform:junit-platform-runner:1.7.1")
    testImplementation group: 'io.netty', name: 'netty-all', version: '4.1.63.Final'
}

test {
    useJUnitPlatform()
}

processResources {
    outputs.upToDateWhen { false }
    filesMatching("system.properties") {
        it.expand("version": parent.properties["version"].toString())
    }
}


publishing {
    repositories {
        maven {
            name = "Reposilite"
            url = uri(rootProject.properties["reposilite.release"] as String)
            credentials {
                username = (rootProject.properties["reposilite.user"] ?: "") as String
                password = (rootProject.properties["reposilite.token"] ?: "") as String
            }
        }
    }

    publications {
        create("jar", MavenPublication) {
            groupId 'skywolf46'
            artifactId "bsl-${project.name}"
            version rootProject.properties['version'] as String
            from components.java
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "milkyway0308_BungeeSwitchListener-Rebirth"
        property "sonar.organization", "milkyway0308"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
